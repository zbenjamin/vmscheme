%{
#include "parser.h"  /* Generated by bison. */

int interactive;
%}

%option nounput
%option noinput
%option noyywrap
%option bison-bridge

DIGIT    [0-9]
SYMCHARS [a-zA-Z!$%&*+-./:<=>?@^_~]
RESERVED [\[\]|]

%%

\(  { return LP; }
\)  { return RP; }

{DIGIT}+  {
  yylval->num = atoi(yytext);
  return NUMBER;
}
{SYMCHARS}({SYMCHARS}|{DIGIT})*  {
  yylval->str = strdup(yytext);
  return SYMBOL;
}

#t { return BOOL_TRUE; }
#f { return BOOL_FALSE; }
' { return QUOTE; }

[ \t]                            /* ignore whitespace */
;.*                              /* ignore comments */
\n  { if (interactive) yyterminate(); }

{RESERVED}  {
  printf("reserved character encountered: %s\n", yytext);
}
.  { printf("unrecognized character encountered: %s\n", yytext);
}

%%

void
set_scanner_interactive(int flag)
{
  interactive = flag;
  yy_set_interactive(flag);
}

void
reset_scanner_start_condition()
{
  BEGIN(0);
}