%{
#include "parser.h"  /* Generated by bison. */

int interactive;
%}

%option nounput
%option noinput
%option noyywrap
%option bison-bridge

DIGIT    [0-9]
LETTER   [a-zA-Z]
SP_INITIAL [!$%&*/:<=>?^_~]
SP_SUBSEQ  [-+.@]
INITIAL  {LETTER}|{SP_INITIAL}
SUBSEQ   {INITIAL}|{DIGIT}|{SP_SUBSEQ}
RESERVED [\[\]|]

%%

\(  { return LP; }
\)  { return RP; }

{DIGIT}+  {
  yylval->num = atoi(yytext);
  return NUMBER;
}

{INITIAL}{SUBSEQ}*|\+|\-|\.\.\.  {
  yylval->str = strdup(yytext);
  return SYMBOL;
}

\"([^\"\\\n]|(\\['\"?\\abfnrtv]))*\"  {
  /* strip off the start and end double quotes */
  yylval->str = strndup(yytext + 1, strlen(yytext) - 2);
  return STRING;
}

#t { return BOOL_TRUE; }
#f { return BOOL_FALSE; }
' { return QUOTE; }
` { return QUASIQUOTE; }
, { return UNQUOTE; }
,@ { return UNQUOTE_SPLICING; }
\. { return DOT; }

[ \t]                            /* ignore whitespace */
;.*                              /* ignore comments */
\n  { if (interactive) yyterminate(); }

{RESERVED}  {
  printf("reserved character encountered: %s\n", yytext);
}
.  { printf("unrecognized character encountered: %s\n", yytext);
}

%%

void
set_scanner_interactive(int flag)
{
  interactive = flag;
  yy_set_interactive(flag);
}

void
reset_scanner_start_condition()
{
  BEGIN(0);
}